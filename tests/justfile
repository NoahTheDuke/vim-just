# To enable using preview recipes from cwd inside tests/
set fallback

# run tests
run FILTER='':
  cargo run {{FILTER}}

# run tests whenever a file changes
watch FILTER='':
  cargo watch \
    --clear \
    --debug \
    --exec "run {{FILTER}}" \
    --watch ..

# install rustup (provides `cargo`)
install-rustup:
  curl --proto '=https' --tlsv1.2 -sSf https://sh.rustup.rs | sh

# install development dependencies
deps:
  cargo install cargo-watch

# compile test runner without running tests
build:
  cargo build

# lint test runner code
lint:
  cargo clippy --all-targets --all-features
  cargo fmt --all -- --check
