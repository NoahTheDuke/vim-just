# To enable using preview recipes from cwd inside tests/
set fallback

# run syntax highlighting tests
run FILTER='':
  cargo run {{ if FILTER == '' { '' } else { '-- ' + quote(FILTER) } }}

# run tests whenever a file changes
watch FILTER='':
  cargo watch \
    --clear \
    --debug \
    --exec "run {{FILTER}}" \
    --watch ..

# run ftdetect tests
ftdetect:
  cargo run --bin=test-ftdetect

# install development dependencies
deps:
  cargo install cargo-watch

# compile test runner without running tests
build:
  cargo build

# for test runner development: strict production-level checks on test runner code
lint:
  cargo clippy --all-targets --all-features -- -Dwarnings
  cargo fmt --all -- --check
